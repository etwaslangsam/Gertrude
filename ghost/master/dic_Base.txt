＊OnCacheRestore
＞OnSatoriLoad
＊OnSatoriLoad
＄文「＊」の重複回避	有効、起動中
＄単語群「＊」の重複回避	有効、起動中
＄自動挿入ウェイトタイプ	里々
＄なでられ反応回数	４５
＄会話時サーフェス戻し	無効
＄デフォルトサーフェス0	100
＄デフォルトサーフェス1	10

＊OnCacheSuspend
＞OnSatoriUnload
＊OnSatoriUnload
＄r_talk	
＄バレンタイン初回	
＄バレンタイン初回年	
＄デフォルトサーフェス2	

＃────────
＊OnSurfaceRestore
＞OnSurfaceRestore_（Reference0）
＃\0\s[（デフォルトサーフェス0）]\e

＊OnSurfaceRestore_-1
\0\s[106]Zzz‥‥\w5 Zzzz‥‥\w9\n
\s[24]Hmmm‥‥\w5 \s[102]oh！\w3 （ユーザ名）？\e

＃────────■バルーン変更
＊OnBalloonChange
\0\s[5]バルーン「（Reference0）」デース！\w5 Wow！\w5 クール！\e

＃────────■最小化解除
＊OnWindowStateMinimize
＄手動セーブ	実行
＊OnWindowStateRestore
＄手動セーブ	実行
＞最小化解除

＊最小化解除
\0\s[24]oh‥‥（ユーザ名）‥‥。\w5
ドラマがいい所だったのに‥‥。\e

＊最小化解除
\0\s[5]Bye mom.\_w[1000]\n
\s[100]（ユーザ名）。\w5
\s[105]向こうにいるマミーとtellしてたデース。\w5
\s[5]元気そうでよかったデース！\e

＊最小化解除
\0\s[106]wow‥‥yum yum‥‥\w5
\s[24]ｹﾞﾌｰ\w9\n
\s[2]（substr,（ユーザ名）,0,1）、（ユーザ名）！？\w5
\s[133]べ、別に何も食べてないデスよ‥‥？\e

＃────────■スクリーンセーバー
＊OnScreenSaverEnd
\0\s[5]（ユーザ名）！\w5 おかえりなさいデース！\e

＊OnSysSuspend
\0\s[106]\e

＊OnSysResume
\0\s[106]...zZZ
......zzZZZ\_w[1200]
\s[34]\c‥‥んあ？\_w[1200]
\s[22]\c（ユーザ名）！？\w5
\s[5]グッモーニン！\e

＊OnDisplayPowerStatus	（Reference0）
\0\s[105]Hi,（ユーザ名）！\e

＃────────■入力
＊メモする
＄メモ	（erase_first,（Reference1）,メモ　）
\0\s[5]ＯＫ！\w5 メモしました！\e
＊メモを読む
\0\s[105]メモを読みマース！\w5\n
（メモ）

＊OnUserInput
＞空打ちされた	（is_empty,（Reference1））
＄（Reference0）	（Reference1）
＞（Reference0）を教えてもらった

＊空打ちされた
\0\s[24]何も書かれてないデース‥‥。\e


＊OnUserInputCancel
＞OnUserInputCancel_（Reference1）
\0\s[102]閉じちゃいマスよ？\e

＊OnUserInputCancel_close
\0\s[3]Cancelデスね。\e

＊OnUserInputCancel_timeout
\0\s[24]（ユーザ名）？\e

＃────────■タイムイベント
＊OnMinuteChange
＞見切れ	（Reference1）==1

＊見切れ
＄見切れてる	
\0\s[3]‥‥‥‥。\e

＊OnHourTimeSignal
＄手動セーブ	実行
＞（現在月）月（現在日）日起動	（現在時）==０
＞時報	（現在分）＜６&&（時報設定）==１

＊時報
＞（現在時）時
\0\s[5]（現在時）時φ:00デース！\e

＊０時
\0\s[24]oh...\w5もう12φ:00 a.m,デス‥‥。\w9\nI'm ベリーベリー スリーピィ‥‥。\e
＊６時
\0\s[5]グッモーニン エブリワン！\w9
朝はKelloggのシリアルで決まりデース！\w9\n
\s[105]（シリアル台詞）\e
＊１２時
\0\s[5]12φ:00 p.m.デース！\w9
今日のランチはなんですカー？\e
＊１８時
\0\s[100]6φ:00 p.m.デス。\w9
‥‥‥‥\s[24]疲れてないデスよ？\e

＠シリアル台詞
グゥーレイトォ！
もう我慢出来ない！！
ぼくのようにでっかくなろう！

＃────────■選択肢
＊OnChoiceSelectEx
＄選択ラベル	（Reference0）
＄選択ＩＤ	（Reference1）
＞（Reference1）
\0\s[24]『（Reference0）』 Not Found.\e

＊OnChoiceTimeout
\0\s[34]（ユーザ名）？

＊OnAnchorSelectEx
＄選択ラベル	（Reference0）
＄選択ＩＤ	（Reference1）
＞OnAnchorSelect.URL	（compare_head,（Reference1）,http://）||（compare_head,（Reference1）,https://）
＞（Reference1）
\0\s[24]『（Reference0）』 Not Found.\e

＊OnAnchorSelect.URL
＄スクリプトの一番頭	\C
\0\j[（Reference1）]

＃────────■インストール反応
＊OnInstallBegin
\0\s[105]インストールもガーティにお任せ！\w9

＊OnInstallComplete
＞OnInstallComplete_（Reference0）
\0（replace（バイト値,１）（Reference1）（バイト値,１）\（バイト値,１）\\）をインストールしましタ。\e

＊OnInstallComplete_shell
\0\s[5]Shell『（replace（バイト値,１）（Reference1）（バイト値,１）\（バイト値,１）\\）』 Install！\e

＊OnInstallComplete_ghost
\0\s[5]Ghost『（replace（バイト値,１）（Reference1）（バイト値,１）\（バイト値,１）\\）』 Install！
ウェルカーム！\w3 歓迎しマース！\e

＊OnInstallComplete_balloon
\0\s[5]Balloon『（replace（バイト値,１）（Reference1）（バイト値,１）\（バイト値,１）\\）』 Install！\e


＊OnInstallFailure
＞OnInstallFailure_（Reference0）


＊OnInstallFailure_extraction
\0\s[24]OMG......\w5 ファイルがブロウクンして無理みたいデース。\e

＊OnInstallFailure_invalid type
\0\s[24]Hmm......\w5install.txtがミスしてるみたいデスね。\e

＊OnInstallFailure_artificial
\0\s[102]ワッツ？！\e

\0\s[24]ソーリィ‥‥。\w5\n（Reference0）で失敗しましタ。\e

＊OnInstallRefuse
\0\s[102]（ユーザ名）！\w5
これ「（replace（バイト値,１）（Reference0）（バイト値,１）\（バイト値,１）\\） へ」って書いてありマース！\w5
\s[30]ちゃんと渡してあげなきゃダメですヨ？\e

＃────────■ファイルドロップイベント
＊OnUpdatedataCreating
\0\s[105]Updateファイルですネ？\w5
\s[5]任せておいてくだサーイ！\_w[1000]

＊OnUpdatedataCreated
\0\s[105]サクセス！\w9
\s[5]更新デスカ？\w5 お疲れさまでーす！\e

＊OnNarCreating
\0\s[100]NARファイルですネ。\w5
\s[5]箱に詰めるだけの簡単なお仕事デース！\_w[1000]

＊OnNarCreated
\0\s[36]出来ましタ。\w5
\s[103]べ、別に何も隠してないデスよ……？\e

＃────────■URLドロップイベント
＊OnURLDropping
\0\s[106]Now Downloading......\e

＊OnURLDropped
\0\s[105]Download Completed！\n
（Reference0）\e

＊OnURLDropFailure
\0\s[24]Download Failure.\n
（Reference1）\e

＃────────■マウスイベント
＊OnMouseClick	（is_empty，（Reference4））&&（equal，（Reference5），0）
＞なにかはなして	（Clicktalk）

＊OnMouseDoubleClick
＞（Reference3）（Reference4）つつかれ	（Reference5）==0

＊OnMouseWheel
＄ころころカウンタ=（ころころカウンタ）+1
＄ころころカウンタ初期化タイマ	３
＞（Reference3）（Reference4）上ころ	（Reference2）>0&&（ころころカウンタ）>3
＞（Reference3）（Reference4）下ころ	（Reference2）<0&&（ころころカウンタ）>3
＞（Reference3）（Reference4）ころころ	（ころころカウンタ）>3

＊ころころカウンタ初期化
＄ころころカウンタ	

＃────────■時計合わせ
＊OnSNTPBegin
\0\s[105]クロックの修理デスね？\w5
任せてくだサーイ！

＊OnSNTPCompare
（whenlist，（Reference0）==
	，0，\0\s[102]ぴったりデース！\e
	，\6
）

＊OnSNTPCorrect
\0\s[5]ミッションコンプリート！\w5デース！\e

＊OnSNTPFailure
\0\s[24]ソーリィ……。
うまく出来ないみたいデース……。\e

＃────────■メールチェック
＊OnBIFFBegin
\0\s[105]メールチェックですネ？\w5
ガーティにお任せアレー！\w9

＊OnBIFFComplete	（Reference0）!=0
\0\s[5]（Reference0）通のメールが届いてましター！

\q[メーラを開く,メーラを開く]
\e

＊メーラを開く
\0\![open,mailer]\e

＊OnBIFFComplete	（Reference0）==0
\0\s[24]ノーゥ……メールは来てなかったデス。\e

＊OnBIFFFailure
\0（whenlist,（Reference0）==
,kick,\s[24]Hmm……ＩＤかパスワードが違うみたいデース。
,timeout,\s[24]サーバーの応答ナッシン……。\w5\n設定ミスか回線が混んでるみたいデース。
,メールチェック失敗。\w5\n原因は「（Reference0）」みたいデース……。
）\e



＃────────バッテリーイベント
＊OnBatteryLow
\0\s[24]バッテリーが（Reference0）％になったデース。\w9
\s[134]あと（Reference1）時間は持つと思いマス。\e

＊OnBatteryCritical
\0\s[31]バッテリーが（Reference0）％になりマシタ。\w9
\s[30]（ユーザ名）、チャージしないんデスカ？\e

＊OnBatteryChargingStart
\0\s[5]バッテリーのチャージ開始デース！\e

＊OnBatteryChargingStop
\0\s[100]チャージ終了デース。\w9
（Reference0）％になりマシタ。\e

＃────────音声認識/音声合成
＊OnVoiceRecognitionWord
＞メインメニュー	（compare,（Reference1）,ガーティ）
＞（Reference1）
\0\s[28]Sorry...うまくリスニングできませんでシタ。\e

＊OnSpeechSynthesisStatus	（compare,（Reference0）,1）
\0\s[100]音声がONになりまシタ。\e

＃────────■キー入力
＊OnKeyPress
＞OnKeyPress_（Reference0）

＊OnKeyPress_c
＞話しかける

＊OnKeyPress_t
＞なにかはなして

＊OnKeyPress_m
＞メインメニュー

＊OnKeyPress_r
＄辞書リロード	実行

＊OnKeyPress_s
＞アクション

＊OnKeyPress_escape
\0\s[102]What！？\![set,windowstate,minimize]

＃────────
＊OnTalk
＞（Reference1）	（Reference0）
（）
